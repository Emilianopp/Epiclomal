import os
import re

name_regions = config['CPG_COORDS']['NAME_REGIONS']
cells = config['cells']
data_ID = config['DATA_ID']

OUTDIR = config['OUTDIR']
LOG_DIR = os.path.join(OUTDIR, 'log')
CPG_COORDS_OUTDIR = os.path.join(OUTDIR, 'CpG_coordinates_in_regions')
CELL_BASED_METH_OUTDIR = os.path.join(OUTDIR, 'cell_based_CpGs')
CELL_BASED_STAT_OUTDIR = os.path.join(OUTDIR, 'cell_based_stats')
STATS_METHYLATION_OUTDIR = os.path.join(OUTDIR, 'final_stats', config['STATS_METHYLATION']['ALL_CUTOFFS'])
FILTER_REGIONS_OUTDIR = os.path.join(OUTDIR, 'filter_regions', config['STATS_METHYLATION']['ALL_CUTOFFS'])
READY_EPICLOMAL_OUTDIR = os.path.join(OUTDIR, 'epiclomal_input', config['STATS_METHYLATION']['ALL_CUTOFFS'])

default_mem=50

if (config['STATS_METHYLATION']['ALL_CUTOFFS']) :
    cutoffs = config['STATS_METHYLATION']['ALL_CUTOFFS']
else:
    cutoffs = "_".join([str(config['STATS_METHYLATION']['NUM_CELLS_CUTOFF']), str(config['STATS_METHYLATION']['MISS_PROP_CUTOFF']), str(config['STATS_METHYLATION']['NLOCI_CUTOFF'])])

CHR = list(range(1, 23))
CHR += ['X', 'Y']

if config['CPG_COORDS']['GENOME_LIBRARY'] == "BSgenome.Hsapiens.UCSC.hg19" or config['CPG_COORDS']['GENOME_LIBRARY'] == "BSgenome.Mmusculus.UCSC.mm10":
    CHR = list('chr' + str(x) for x in CHR)

if not os.path.exists(LOG_DIR):
    os.makedirs(LOG_DIR)

rule all:
    input:
        regions=os.path.join(READY_EPICLOMAL_OUTDIR, cutoffs, 'regionIDs_input_Epiclomal_{}.tsv.gz'.format(data_ID)),
        meth_file=os.path.join(READY_EPICLOMAL_OUTDIR, cutoffs, 'input_Epiclomal_{}.tsv.gz'.format(data_ID))

rule get_cpgs_in_regions:
    input:
        regions_file=config['REGION_FILE']
    output:
        touch(os.path.join(OUTDIR, '.get_cpgs_in_regions_{CHR}'))
    resources:
        h_vmem=default_mem
    params:
        qsub_out=os.path.join(LOG_DIR, '{CHR}_cpgs_qsub_out'),
        qsub_err=os.path.join(LOG_DIR, '{CHR}_cpgs_qsub_err')
    shell:
        'mkdir -p {CPG_COORDS_OUTDIR}; '
        'Rscript {config[PROCESS_REAL_DATA_DIR]}/CpG_coordinates_across_regions.R '
        '--output_directory {CPG_COORDS_OUTDIR} '
        '--regions_file {input.regions_file} '
        '--name_regions {config[CPG_COORDS][NAME_REGIONS]} '
        '--genome_library {config[CPG_COORDS][GENOME_LIBRARY]} '
        '--type_of_C {config[CPG_COORDS][TYPE_OF_C]} '
        '--chr {wildcards.CHR}'

rule cell_based_methylation_extraction:
    input:
        path_cell_data=config['DATA_DIR'],
        get_cpgs_in_regions=expand('.get_cpgs_in_regions_{CHR}', CHR=CHR)
    output:
        os.path.join(CELL_BASED_METH_OUTDIR, "CpG_meth_data_long_" + data_ID + "_{cells}.tsv.gz")
    resources:
        h_vmem=120
    params:
        qsub_out=os.path.join(LOG_DIR, 'cell_based_meth_qsub_out'),
        qsub_err=os.path.join(LOG_DIR, 'cell_based_meth_qsub_err')
    shell:
        'mkdir -p {CELL_BASED_METH_OUTDIR}; '
        'Rscript {config[PROCESS_REAL_DATA_DIR]}/cell_based_methylation_extraction.R '
        '--output_directory {CELL_BASED_METH_OUTDIR} '
        '--data_ID {data_ID} '
        '--path_CpG_coordinates {CPG_COORDS_OUTDIR} '
        '--path_cell_data {input.path_cell_data} '
        '--cell_ID {wildcards.cells} '
        '--data_type {config[CELL_BASED_METH][DATA_TYPE]} '
        '--genome {config[CELL_BASED_METH][GENOME]} '
        '--include_chrY {config[CELL_BASED_METH][INCLUDE_CHRY]}'

rule cell_based_stats:
    input:
        post_processed_CpG_data_file=os.path.join(CELL_BASED_METH_OUTDIR, "CpG_meth_data_long_" + data_ID + "_{cells}.tsv.gz")
    output:
        os.path.join(CELL_BASED_STAT_OUTDIR, "stats_region_" + data_ID + "_{cells}.tsv")
    resources:
        h_vmem=default_mem
    params:
        qsub_out=os.path.join(LOG_DIR, 'cell_based_stats_qsub_out'),
        qsub_err=os.path.join(LOG_DIR, 'cell_based_stats_qsub_err')
    shell:
        'mkdir -p {CELL_BASED_STAT_OUTDIR}; '
        'Rscript {config[PROCESS_REAL_DATA_DIR]}/cell_based_stats_methylation.R '
        '--output_directory {CELL_BASED_STAT_OUTDIR} '
        '--data_ID {data_ID} '
        '--post_processed_CpG_data_file {input.post_processed_CpG_data_file} '
        '--cell_ID {wildcards.cells}'

rule stats_methylation:
    input:
        cell_based_CpGs=expand(os.path.join(CELL_BASED_METH_OUTDIR, "CpG_meth_data_long_" + data_ID + "_{cells}.tsv.gz"), cells=cells),
        cell_based_stats=expand(os.path.join(CELL_BASED_STAT_OUTDIR, "stats_region_" + data_ID + "_{cells}.tsv"), cells=cells),
        get_cpgs_in_regions=expand('.get_cpgs_in_regions_{CHR}', CHR=CHR)
    output:
        os.path.join(STATS_METHYLATION_OUTDIR, cutoffs, "final_mean_meth_region_" + data_ID + ".tsv"),
        os.path.join(STATS_METHYLATION_OUTDIR, cutoffs, "final_regions_" + data_ID + ".tsv")
    resources:
        h_vmem=120
    params:
        qsub_out=os.path.join(LOG_DIR, 'stats_meth_qsub_out'),
        qsub_err=os.path.join(LOG_DIR, 'stats_meth_qsub_err')
    shell:
        'Rscript {config[PROCESS_REAL_DATA_DIR]}/stats_methylation.R '
        '--output_directory {STATS_METHYLATION_OUTDIR} '
        '--data_ID {data_ID} '
        '--path_post_processed_CpG_data {CELL_BASED_METH_OUTDIR} '
        '--path_stats_region_data {CELL_BASED_STAT_OUTDIR} '
        '--num_cells_cutoff {config[STATS_METHYLATION][NUM_CELLS_CUTOFF]} '
        '--miss_prop_cutoff {config[STATS_METHYLATION][MISS_PROP_CUTOFF]} '
        '--nloci_cutoff {config[STATS_METHYLATION][NLOCI_CUTOFF]} '
        '--all_cutoffs {config[STATS_METHYLATION][ALL_CUTOFFS]} '
        '--filter_regions_same_meth {config[STATS_METHYLATION][FILTER_REGIONS_SAME_METH]} '
        '--plot_heatmap_unfiltered {config[STATS_METHYLATION][PLOT_HEATMAP_UNFILTERED]} '
        '--plot_heatmap_filtered {config[STATS_METHYLATION][PLOT_HEATMAP_FILTERED]}'

rule filter_regions:
    input:
        meth_file=os.path.join(STATS_METHYLATION_OUTDIR, cutoffs, "final_mean_meth_region_" + data_ID + ".tsv"),
        regions_file=os.path.join(STATS_METHYLATION_OUTDIR, cutoffs, "final_regions_" + data_ID + ".tsv")
    output:
        os.path.join(FILTER_REGIONS_OUTDIR, cutoffs, "filtered_regions_" + data_ID + ".tsv")
    resources:
        h_vmem=120
    params:
        qsub_out=os.path.join(LOG_DIR, 'filter_regions_qsub_out'),
        qsub_err=os.path.join(LOG_DIR, 'filter_regions_qsub_err')
    run:
        if (config['FILTER_REGIONS']['FILTER']):
            shell('Rscript {config[PROCESS_REAL_DATA_DIR]}/filter_regions.R '
                '--output_directory {FILTER_REGIONS_OUTDIR} '
                '--data_ID {data_ID} '
                '--mean_methylation_file {input.meth_file} '
                '--true_file {config[TRUE_FILE]} '
                '--coef_threshold {config[FILTER_REGIONS][COEF_THRESHOLD]} '
                '--mean_diff_threshold {config[FILTER_REGIONS][MEAN_DIFF_THRESHOLD]}'
            )
        else:
            shell('cp {input.regions_file} {output}')

rule get_data_ready_Epiclomal:
    input:
        cell_based_stats=expand(os.path.join(CELL_BASED_STAT_OUTDIR, "stats_region_" + data_ID + "_{cells}.tsv"), cells=cells),
        file_final_regions=os.path.join(FILTER_REGIONS_OUTDIR, cutoffs, "filtered_regions_" + data_ID + ".tsv")
    output:
        regions=os.path.join(READY_EPICLOMAL_OUTDIR, cutoffs, 'regionIDs_input_Epiclomal_' + data_ID + '.tsv.gz'),
        meth_file=os.path.join(READY_EPICLOMAL_OUTDIR, cutoffs, 'input_Epiclomal_' + data_ID + '.tsv.gz')
    resources:
        h_vmem=480
    params:
        qsub_out=os.path.join(LOG_DIR, 'prepare_epiclomal_qsub_out'),
        qsub_err=os.path.join(LOG_DIR, 'prepare_epiclomal_qsub_err')
    shell:
        'Rscript {config[PROCESS_REAL_DATA_DIR]}/get_data_ready_Epiclomal.R '
        '--output_directory {READY_EPICLOMAL_OUTDIR} '
        '--data_ID {data_ID} '
        '--path_post_processed_CpG_data {CELL_BASED_METH_OUTDIR} '
        '--file_final_regions {input.file_final_regions} '
        '--filter_CpG_no_data {config[READY_EPICLOMAL][FILTER_CPG_NO_DATA]} '
        '--LuoDiamond {config[READY_EPICLOMAL][LUODIAMOND]}'


